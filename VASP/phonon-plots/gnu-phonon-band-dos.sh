#!/opt/local/bin/gnuplot
# This plotiing script written by Chandan
# Use to plot phonon band and dos
# generated by phonopy
set terminal postscript enhanced color solid lw 1.2 "Times-Roman" 24
set output "phonon-band-dos.eps"
set key font "Times-Roman,24" top center
set nolabel
set notitle
set encoding utf8
set multiplot layout 1,2
set size 1,1
###################Plot band structure##########################################################
set origin 0,0
set size 0.7,1.0
set noxtics
set format y "%2.1f"
ymin=-0.2
ymax= 7.0
# Change the xtics depending upon path
X0=0.0
X1=0.15219320
X2=0.30438650
X3=0.48012410
X4=0.63231730
set yrange [ymin:ymax]
set ytics 1
set ylabel "Frequency (THz)" font "Times-Roman,24" 
set style line 1 lt 2 lc rgb 'red'
set xtics ("{/Symbol G}" X0, "X" X1, "M" X2, "{/Symbol G}" X3, "R" X4)
set arrow 100 from X0,ymin to X0,ymax nohead lc rgb "#C0C0C0"
set arrow 200 from X1,ymin to X1,ymax nohead lc rgb "#C0C0C0"
set arrow 300 from X2,ymin to X2,ymax nohead lc rgb "#C0C0C0"
set arrow 400 from X3,ymin to X3,ymax nohead lc rgb "#C0C0C0"
set arrow 500 from X4,ymin to X4,ymax nohead lc rgb "#C0C0C0"
set xzeroaxis lt 2 lw 1 lc rgb "#A0A0A0"
plot 'phonon.dat' u ($1):($2) notitle with lines lt 1 lc 0 lw 1.5

#####################################Plot density of states########################################
#
set origin 0.590, 0.0
set size 0.4,1.0
unset arrow
unset xrange
unset yrange
unset ylabel
unset xlabel
set nokey
set nolabel
#set title "spin-down channel"
set encoding utf8
unset xtics
unset ytics
unset label
unset xrange
unset yrange
set key font "Times-Roman,22" samplen 1.5 at 0.8,1
set mxtics 5
set mytics 5
#set xlabel "DOS(states/eV/unitcell)"
ymin=-0.2
ymax= 7.0
set yrange [ymin:ymax]
unset ytics
#set ylabel "Energy (eV)"
set xrange[0:2]
set xtics 1
plot 'total_dos.dat' u ($2):($1) title 'TDOS' w line lt 1 lc 0 lw 1.5,\
'projected_dos.dat' u ($2):($1) title 'Na ' w line lt 1 lc rgb "#FF0000" lw 1.5,\
'projected_dos.dat' u ($3):($1) title 'Cl ' w line lt 1 lc rgb "#0000FF" lw 1.5
